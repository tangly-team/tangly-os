/*
 * Copyright 2006-2021 Marcel Baumann
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES
 * OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

/**
 * The set of gradle command line special commands will seldom use:
 * To publish all modules in local maven repository         gradle publishMavenJavaPublicationToMavenLocal
 * To publish all module in maven central repository        gradle publishMavenJavaPublicationToMavenRepository -Pmode=prod
 * To generate aggregate code coverage metrics              gradle build jacocoTestReport jacocoRootReport
 * To generate static and aggregate code coverage metrics   gradle build jacocoTestReport jacocoRootReport -Pmode=int
 * To detect vulnerab=ilities                               gradle ossIndexAudit --info -Pmode=int
 * To detect newer versions of libraries                    gradle dependencyUpdates -Drevision=release
 * To update sonar cloud                                    gradle sonarqube -Dsonar.login=<login token>
 * To create a production release                           gradle build -Pvaadin.productionMode
 */
plugins {
    id "java-library"
    id 'maven-publish'
    id 'signing'
    id "jacoco"
    id "com.github.spotbugs" version "4.7.5"
    id "org.sonarqube" version "3.3"
    id 'org.sonatype.gradle.plugins.scan' version '2.1.0'
    id "info.solidsoft.pitest" version "1.6.0"
    id 'com.google.cloud.tools.jib' version '3.1.4'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

ext {
    // support conditional gradle script execution for dev, int, prod. Used for example to disable slow reporting plugins
    mode = "dev"
}

repositories {
    maven { setUrl("https://plugins.gradle.org/m2/") }
}

apply plugin: 'jacoco'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    if (mode.equalsIgnoreCase('int')) {
        apply plugin: 'com.github.spotbugs'
        apply plugin: 'org.sonatype.gradle.plugins.scan'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'
        // apply plugin: 'info.solidsoft.pitest'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://maven.vaadin.com/vaadin-addons" }
        maven { url "https://raw.github.com/ZUGFeRD/mustangproject/mvn-repo" }
        maven { url "https://repo.microstream.one/repository/maven-public/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    pitest {
        junit5PluginVersion.set('0.12')
        targetClasses.set(['net.tangly.*'])
        threads.set(8)
        outputFormats.set(['XML', 'HTML'])
        timestampedReports.set(false)
    }

    if (mode.equalsIgnoreCase('int')) {
        pmd {
            ruleSetFiles = files(rootProject.file('src/main/resources/pmd-tangly.xml'))
            toolVersion = "6.33.0"
            ignoreFailures = true
            incrementalAnalysis = true
            rulesMinimumPriority = 3
        }
        spotbugs {
            ignoreFailures = true
            effort = "max"
            reportLevel = "low"
            toolVersion = '4.2.3'
        }
        [spotbugsMain, spotbugsTest].each { task ->
            task.configure {
                reports {
                    html {
                        enabled = true
                        stylesheet = 'fancy-hist.xsl'
                    }
                }
            }
        }
        tasks.withType(Pmd) {
            reports {
                xml.enabled false
                html.enabled true
            }
        }
    }
    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            html.enabled true
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "tangly-os-at-tangly.net"
        property "sonar.organization", "tangly-os"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.language", "java"
        property "sonar.java.source", "1"
        property "sonar.coverage.jacoco.reportPath5s", "**/build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

tasks.register("jacocoRootReport", JacocoReport) {
    subprojects { subproject ->
        subproject.plugins.withType(JacocoPlugin).configureEach {
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach { testTask ->
                sourceSets subproject.sourceSets.main
                executionData(testTask)
            }
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach {
                rootProject.tasks.jacocoRootReport.dependsOn(it)
            }
        }
    }
    reports {
        xml.enabled true
        html.enabled true
    }
}

subprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(16)
        }
        withJavadocJar()
        withSourcesJar()
    }

    ext {
        assertjVersion = '3.18.1'
    }

    configurations.all {
        resolutionStrategy {
            // necessary to avoid compile errors with MustangProject library
            force "org.assertj:assertj-core:$assertjVersion"
            // necessary to avoid JRuby errors because Jackson is loading version 1.19 with an API error
            force "org.yaml:snakeyaml:1.27"
        }
    }

    dependencies {
        implementation(libs.apache.log4j2.api)
        implementation(libs.guice)

        compileOnly(libs.annotations)
        runtimeOnly(libs.apache.log4j2.core)
        runtimeOnly(libs.apache.log4j2.slf4j)
        testImplementation(libs.bundles.test.deps)
        testCompileOnly(libs.annotations)
        testRuntimeOnly(libs.apache.log4j2.core)
        testRuntimeOnly(libs.apache.log4j2.slf4j)
        testRuntimeOnly(libs.junit.engine)
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.fork = true
        options.incremental = true
        options.warnings = true
        options.deprecation = true
        options.compilerArgs += ["-parameters", "--enable-preview", "-Xlint:all,-serial,-varargs", "-Xdoclint:none"]
    }

    tasks.withType(Test) {
        finalizedBy jacocoTestReport
        useJUnitPlatform()
        jvmArgs += ['--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED', '--enable-preview', '-XX:+ShowCodeDetailsInExceptionMessages']
        reports.junitXml.mergeReruns = true
        options {
            setExcludeTags(['integration'] as Set)
        }
        maxParallelForks = 8
        failFast = false
    }

    tasks.withType(Javadoc) {
        enabled = true
        javadoc.options {
            encoding("UTF-8")
            addBooleanOption('-Xdoclint:none')
            addBooleanOption('-enable-preview', true)
            addStringOption('-release', '16')
        }
    }

    tasks.withType(JavaExec) {
        jvmArgs += ['--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED', '--enable-preview', '-XX:+ShowCodeDetailsInExceptionMessages']
    }

    jar {
        doFirst {
            manifest {
                attributes(
                    'Name': project.name,
                    'Automatic-Module-Name': project.ext.moduleName,
                    'Specification-Title': "tangly open source library $project.ext.artifactId",
                    'Specification-Version': project.version,
                    'Specification-Vendor': "tangyly llc, Switzerland (https://www.tangly.net)",
                    'License': 'Apache License v2.0',
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': "tangyly llc, Switzerland (https://www.tangly.net)",
                    'Build-Jdk': "${System.properties['java.version']}",
                    "Class-Path": configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
                )
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                afterEvaluate {
                    groupId = project.ext.groupId
                    artifactId = project.ext.artifactId
                    version = project.version
                }
                from components.java
                pom {
                    afterEvaluate {
                        name = project.ext.moduleName
                        description = project.description
                        url = 'https://tangly-team.bitbucket.io/'
                        inceptionYear = project.ext.inceptionYear
                    }
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'marcelbaumann'
                            name = 'Marcel Baumann'
                            email = 'marcel.baumann@tangly.net'
                            organization = 'tangly llc'
                            organizationUrl = 'https://www.tangly.net'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://bitbucket.org/tangly-team/tangly-os.git'
                        developerConnection = 'scm:git:https://bitbucket.org/tangly-team/tangly-os.git'
                        url = 'https://bitbucket.org/tangly-team/tangly-os.git'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                if (mode.equalsIgnoreCase('prod')) {
                    credentials {
                        // credentials are stored under ~/.gradle/gradle.properties and are first copied in local variables to avoid error if not defined fpr
                        // example in CI pipeline (private keys should not be uploaded to a cloud CI)
                        credentials {
                            username mavenCentralUsername
                            password mavenCentralPassword
                        }
                    }
                }
            }
        }
    }

    if (mode.equalsIgnoreCase('prod')) {
        // currently credentials are not available in the CI pipeline therefore the configuration parameter to avoid spurious execution errors.
        signing {
            // credentials for signing are stored under ~/.gradle/gradle.properties
            // gpg --export-secret-keys > secring.gpg
            // gpg --no-default-keyring --keyring ./secring.gpg --list-keys --keyid-format short
            sign publishing.publications.mavenJava
        }
    }
}
