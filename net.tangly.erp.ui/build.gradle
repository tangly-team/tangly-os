/*
 * Copyright 2006-2022 Marcel Baumann
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES
 * OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

plugins {
    id 'java-app-conventions'
    alias(libs.plugins.vaadin)
    id 'jacoco-report-aggregation'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

project.ext.groupId = 'net.tangly.erp'
project.ext.artifactId = 'ui'
project.version = "0.2.7"
project.ext.moduleName = "${project.ext.groupId}.${project.ext.artifactId}"
project.ext.inceptionYear = '1996'
project.description = "User Interface ERP - using Java 17 or higher (tangly llc)"

application {
    applicationDefaultJvmArgs = ['--enable-preview',
                                 '--add-exports=java.base/jdk.internal.misc=ALL_UNNAMED',
                                 '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
                                 '-XX:+ShowCodeDetailsInExceptionMessages',
                                 '-Xmx256m']
    mainClassName = "net.tangly.erp.ui.EmbeddedJetty"
}

vaadin {
    optimizeBundle = false
}

jar {
    // needed for the shadow jar otherwise get a runtime error in LogManager.getLogger() when starting the application
    manifest {
        attributes 'Multi-Release': 'true'
    }
}

shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
    archiveVersion.set('')
}

dependencies {
    implementation project(':net.tangly.commons')
    implementation project(':net.tangly.core')
    implementation project(':net.tangly.erp.crm')
    implementation project(':net.tangly.erp.invoices')
    implementation project(':net.tangly.erp.ledger')
    implementation project(':net.tangly.erp.products')
    implementation project(':net.tangly.gleam')
    implementation project(':net.tangly.ui')

    implementation enforcedPlatform(libs.vaadin.bom)
    implementation(libs.vaadin.core)
    implementation(libs.apache.log4j2.slf4j)
    implementation(libs.apache.commons.cli)

    implementation(libs.bundles.jetty.deps)
    implementation(libs.vaadin.socharts)
    implementation(libs.bundles.microstream.deps)
    implementation(libs.bundles.jersey.deps)

    testImplementation(libs.vaadin.test)
}
