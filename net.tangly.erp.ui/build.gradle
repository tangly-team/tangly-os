/*
 * Copyright 2006-2022 Marcel Baumann
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 *          https://apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES
 * OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

plugins {
    id 'java-app-conventions'
    alias(libs.plugins.vaadin)
    id 'jacoco-report-aggregation'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

project.ext.groupId = 'net.tangly.erp'
project.ext.artifactId = 'ui'
project.version = "${projectVersion}"
project.ext.moduleName = "${project.ext.groupId}.${project.ext.artifactId}"
project.ext.inceptionYear = '1996'
project.description = "User Interface ERP - using Java 21 or higher (tangly llc)"

// beware the shared archive attributes slow down the application the first time it is called.

application {
    applicationDefaultJvmArgs = ['--enable-preview',
                                 '--add-exports=java.base/jdk.internal.misc=ALL_UNNAMED',
                                 '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
                                 '--add-opens=java.base/java.io=ALL-UNNAMED',
                                 '-XX:+ShowCodeDetailsInExceptionMessages',
                                 '-XX:+AutoCreateSharedArchive',
                                 '-XX:SharedArchiveFile=tangly-os-archive.jsa',
                                 '-Xmx256m',
                                 '-ea']
    mainClassName = "net.tangly.erp.ui.Main"
}

jar {
    // needed for the shadow jar otherwise get a runtime error in LogManager.getLogger() when starting the application
    manifest {
        attributes 'Multi-Release': 'true'
    }
}

shadowJar {
    // the merge provides the required configuration files for Jetty, the archive version is removed to have a stable generated jar filename.
    zip64 true
    mergeServiceFiles()
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
    archiveVersion.set('')
}

distributions {
    main {
        contents {
            from "src/main/config/app-in-memory-no-imports.properties"
            from "../src/main/licenses/apache-2-0-license"
        }
    }
}

dependencies {
    implementation project(':net.tangly.commons')
    implementation project(':net.tangly.core')
    implementation project(':net.tangly.erp.crm')
    implementation project(':net.tangly.erp.invoices')
    implementation project(':net.tangly.erp.ledger')
    implementation project(':net.tangly.erp.products')
    implementation project(':net.tangly.gleam')
    implementation project(':net.tangly.ui')

    implementation(libs.vaadin.core)
    implementation(libs.vaadin.embedded.jetty)
    implementation(libs.vaadin.socharts)
    implementation(libs.apache.log4j2.core)
    implementation(libs.apache.log4j2.slf4j)
    implementation(libs.apache.commons.cli)
    implementation(libs.bundles.microstream.deps)

    testImplementation(libs.vaadin.test)
}
